/*
 * CD meta-model
 *
 * Model for the class diagram meta-model, as defined in the UML.ecore Ecore meta-model.
 *
 * Demonstration of the models generated by the CD2DBS QVT-r bidirectional transformation.
 *
 * Used as running example in
 * [1] N. Macedo, T. GuimarÃ£es and A. Cunha. Model repair and transformation with Echo. ASE 2013.
 * 
 * author: N. Macedo, 10/2012
 */
module CD [Model]

sig CD extends Model {}

abstract sig NamedEntity {
  NamedEntity_ : set CD, 
  name : String -> CD
}

sig Package extends NamedEntity {   
  Package_ : set CD, 
  classes : Class -> CD, 
  associations : Association -> CD
}

sig Class extends NamedEntity { 
  Class_ : set CD, 
  attributes : Attribute -> CD, 
  general : Class -> CD, 
  persistent : set CD 
}

sig Attribute extends NamedEntity { 
  Attribute_ : set CD
}

sig Association extends NamedEntity { 
  Association_ : set CD, 
  source, target : Class -> CD
}

fact LocalState {
  all m : CD { 
     name.m in NamedEntity_.m -> one String
     classes.m in Package_.m one -> Class_.m 
     associations.m in Package_.m one -> Association_.m 
     general.m in Class_.m -> lone Class_.m 
     attributes.m in Class_.m one -> Attribute_.m 
     persistent.m in Class_.m 
     target.m in Association_.m -> one Class_.m 
     source.m in Association_.m -> one Class_.m 

     Package_.m + Class_.m + Attribute_.m + Association_.m = NamedEntity_.m
  }
}

pred Conforms[m : CD] {
  all self : Class_.m | self not in self.^(general.m)
}

run Conforms for 3 but 1 CD
