/*
 * HSM meta-model
 *
 * Model for the hierarchical state machines meta-model, as defined in the HSM.ecore Ecore meta-model.
 *
 * Demonstration of the models generated by the HSM2NHSM QVT-r and ATL bidirectional transformations.
 *
 * The graph-edit distance is defined to calculate model distances in transformations.
 *
 * Used as running example in
 * [1] N. Macedo and A. Cunha. Least-change bidirectional model transformation with QVT-R and ATL. SoSyM 2016
 * 
 * author: N. Macedo, 10/2012
 */
module HSM [Model]

sig SMachine {
	SMachine_ : set Model,
	name : String -> Model,
	states : State -> Model,
	transitions : Transition -> Model,
}

sig State {
	State_ : set Model,
	name : String -> Model,
	container : CompositeState -> Model
}

sig CompositeState extends State {
	CompositeState_ : set Model
}

sig Transition {
	Transition_ : set Model,
	source : State -> Model,
	target : State -> Model
}

fact Relations {
	all m : Model {
		SMachine<:name.m in (SMachine_.m -> one String)
		states.m in (SMachine_.m some -> State_.m)
		transitions.m in (SMachine_.m some -> Transition_.m)
		State<:name.m in (State_.m -> one String)
		CompositeState_.m in State_.m
		container.m in (State_.m -> lone CompositeState_.m)
		source.m in (Transition_.m -> one State_.m)
		target.m in (Transition_.m -> one State_.m)

		no iden & ^(container.m)

		no SMachine - SMachine_.Model
		no State - State_.Model
		no Transition - Transition_.Model

	}
}

fun Delta [hsm,hsm' : Model] : Int {
	(#((SMachine_.hsm - SMachine_.hsm') + (SMachine_.hsm' - SMachine_.hsm))).plus[
	(#((SMachine<:name.hsm - SMachine<:name.hsm') + (SMachine<:name.hsm' - SMachine<:name.hsm))).plus[
	(#((states.hsm - states.hsm') + (states.hsm' - states.hsm)))].plus[
	(#((transitions.hsm - transitions.hsm') + (transitions.hsm' - transitions.hsm))).plus[
	(#((State<:name.hsm - State<:name.hsm') + (State<:name.hsm' - State<:name.hsm))).plus[
	(#((container.hsm - container.hsm') + (container.hsm' - container.hsm))).plus[
	(#((source.hsm - source.hsm') + (source.hsm' - source.hsm))).plus[
	(#((target.hsm - target.hsm') + (target.hsm' - target.hsm)))]]]]]]}
