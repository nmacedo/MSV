MODULE main

-- Fox, goose and bag of beans puzzle
-- https://en.wikipedia.org/wiki/Fox,_goose_and_bag_of_beans_puzzle
-- Author: Nuno Macedo

VAR
    -- the position of the farmer and his purchases
    -- either on the starting bank of the river (FALSE) or on the far bank (TRUE)
    farmer : boolean;
    beans : boolean;
    goose : boolean;
    fox : boolean;

-- only allow transitions if the purchase is in the same side as the farmer
TRANS
    OP = g -> goose = farmer;
TRANS
    OP = f -> fox = farmer;
TRANS
    OP = b -> beans = farmer;

-- only allow transitions that do not lead to dangerous situations
TRANS
	! (next(goose) = next(beans) & next(goose) != next(farmer));
TRANS
	! (next(goose) = next(fox) & next(goose) != next(farmer));

IVAR
	-- the set of valid operations as external input
	-- transport the g(oose), f(ox), b(eans) or a(lone)
	OP : {g,f,b,a};

ASSIGN 
    -- everyone at the starting river bank
	init (goose) := FALSE;
	init (fox) := FALSE;
	init (beans) := FALSE;
	init (farmer) := FALSE;

    -- the farmer always crosses the river
	next(farmer) := !farmer;

	-- the purchases cross the river if appropiate operation and farmer on the same bank
	next(goose) := case
					OP = g : !goose;
					TRUE : goose;
				   esac;

	next(fox) := case
					OP = f : !fox;
					TRUE : fox;
				   esac;

	next(beans) := case
					OP = b : !beans;
					TRUE : beans;
				   esac;

-- is it possible to cross everyone at all? 
-- transitions leading to dangerous scenarios were forbidden
LTLSPEC G ! (goose & fox & beans);
